#
# A stripped-down sorin theme.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#   Josh Symonds <josh@joshsymonds.com>
#
# Screenshots:
#   http://i.imgur.com/nBEEZ.png
#

# Load dependencies.
pmodload 'helper'

function prompt_josh_pwd {
  local pwd="${PWD/#$HOME/~}"

  if [[ "$pwd" == (#m)[/~] ]]; then
    _prompt_josh_pwd="$MATCH"
    unset MATCH
  else
    _prompt_josh_pwd="${${${(@j:/:M)${(@s:/:)pwd}##.#?}:h}%/}/${pwd:t}"
  fi
}

function prompt_josh_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Format PWD.
  prompt_josh_pwd

  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi

  # Get ruby information
  if (( $+functions[ruby-info] )); then
    ruby-info
  fi
}

function prompt_josh_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_josh_precmd

  # Prompt colors
  black="%F{0}";
  darkgrey="%F{8}";
  blue="%F{4}";
  cyan="%F{6}";
  green="%F{2}";
  orange="%F{208}";
  purple="%F{4}";
  red="%F{1}";
  violet="%F{13}";
  white="%F{15}";
  yellow="%F{3}";
  turquoise="%F{81}"
  purple2="%F{135}"
  hotping="%F{161}"
  limegreen="%F{118}"

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format '${red}...%f%b'

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:branch' format '${turquoise}%b%f${git_info[rprompt]}'
  zstyle ':prezto:module:git:info:action' format '${yellow}%f'
  zstyle ':prezto:module:git:info:added' format "${green}✚%f"
  zstyle ':prezto:module:git:info:ahead' format "${yellow}⬆%f"
  zstyle ':prezto:module:git:info:behind' format "${yellow}⬇%f"
  zstyle ':prezto:module:git:info:commit' format "${green}%.7c%f"
  zstyle ':prezto:module:git:info:deleted' format "${red}✖%f"
  zstyle ':prezto:module:git:info:modified' format "${blue}✱%f"
  zstyle ':prezto:module:git:info:position' format "${red}%p%f"
  zstyle ':prezto:module:git:info:renamed' format "${magenta}➜%f"
  zstyle ':prezto:module:git:info:stashed' format "${cyan}✭%f"
  zstyle ':prezto:module:git:info:unmerged' format "${yellow}═%f"
  zstyle ':prezto:module:git:info:untracked' format "${white}◼%f"
  zstyle ':prezto:module:git:info:keys' format \
    'prompt' '($(coalesce "%b" "%p" "%c")%s)' \
    'rprompt' '%A%B%S%a%d%m%r%U%u'


  # Define prompts.
  PROMPT='
${turquoise}# ${orange}%n%f @ ${yellow}%m%f in ${green}%~%f ${(e)git_info[prompt]}
${green}→ '

  SPROMPT='zsh: correct ${red}%R%f to ${green}%r%f [nyae]? '
}

prompt_josh_setup "$@"

